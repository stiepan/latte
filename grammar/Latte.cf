-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type PIdent "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type PIdent;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Block ::= POpenBlock [Stmt] PCloseBlock ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= PIdent ; 

Init.      Item ::= PIdent "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= PIdent "=" Expr  ";" ;

Incr.      Stmt ::= PIdent "++"  ";" ;

Decr.      Stmt ::= PIdent "--"  ";" ;

Ret.       Stmt ::= PReturn Expr ";" ;

VRet.      Stmt ::= PReturn ";" ;

Cond.      Stmt ::= PIf "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= PIf "(" Expr ")" Stmt PElse Stmt  ;

While.     Stmt ::= PWhile "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= PIdent ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= PIdent "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= PMinus Expr6 ;

Not.       Expr5 ::= PNot Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 PAnd Expr1 ;

EOr.       Expr ::= Expr1 POr Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= PPlus ;

internal   OverloadedPlus. AddOp ::= Type PPlus ;

Minus.     AddOp ::= PMinus ;

Times.     MulOp ::= PTimes ;

Div.       MulOp ::= PDiv ;

Mod.       MulOp ::= PMod ;

LTH.       RelOp ::= PLTH ;

LE.        RelOp ::= PLE ;

GTH.       RelOp ::= PGTH ;

GE.        RelOp ::= PGE ;

EQU.       RelOp ::= PEQU ;

internal   OverloadedEQU. RelOp ::= Type PEQU ;

NE.        RelOp ::= PNE ;

internal   OverloadedNE. RelOp ::= Type PNE ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

-- tokens --------------------------------------------------

position token POpenBlock {"{"} ;
position token PCloseBlock {"}"} ;
position token PIf {"if"} ;
position token PElse {"else"} ;
position token PWhile {"while"} ;
position token PReturn {"return"} ;

position token PTrue {"true"} ;
position token PFalse {"false"} ;
position token PEQU {"=="} ;
position token PLE {"<="} ;
position token PGE {">="} ;
position token PNE {"!="} ;
position token PAnd {"&&"} ;
position token POr {"||"} ;
position token PMinus {"-"} ;
position token PNot {"!"} ;
position token PPlus {"+"} ;
position token PTimes {"*"} ;
position token PDiv {"/"} ;
position token PMod {"%"} ;
position token PLTH {"<"} ;
position token PGTH {">"} ;

position token PIdent (letter (letter | digit | '_' | '\'' )*);

